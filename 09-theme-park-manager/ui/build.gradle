import com.gradlehero.themepark.FileDiffTask

plugins {
    id 'java-library'
}

// the following generates downloadable files for lesson "Practical: creating a multi-project build" so can be ignored
tasks.register('downloadable', Copy) {
    from parent.layout.projectDirectory
    include "${project.name}/build.gradle"
    include "${project.name}/src/**"
    into layout.buildDirectory.dir('course-downloadable')
}

tasks.register('downloadableZip', Zip) {
    from parent.layout.projectDirectory
    include "${project.name}/build.gradle"
    include "${project.name}/src/**"
    archiveFileName = 'ui.zip'
    destinationDirectory = layout.buildDirectory
}

tasks.named('assemble').configure {
    dependsOn tasks.named('downloadable'), tasks.named('downloadableZip')
}

apply plugin: FileDiffPlugin

fileDiff {
    file1 = file('src/main/resources/static/images/teacups.jpg')
    file2 = file('src/main/resources/static/images/logflume.jpg')
}

class FileDiffPlugin implements Plugin<Project> {
    void apply(Project project) {
        FileDiffPluginExtension extension = project.extensions.create('fileDiff', FileDiffPluginExtension)
        project.tasks.register('fileDiff', FileDiffTask) {
            file1 = extension.getFile1()
            file2 = extension.getFile2()
        }
    }
}

abstract class FileDiffPluginExtension {
    abstract RegularFileProperty getFile1()
    abstract RegularFileProperty getFile2()
}

